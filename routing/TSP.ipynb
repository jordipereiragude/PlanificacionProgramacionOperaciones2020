{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using JuMP\n",
    "using CPLEX"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "#matriz de distancias aleatorias\n",
    "function instanciaAleatoria(n::Int64)\n",
    "    l = rand(1:1000, n*n)\n",
    "    for i in 1:n\n",
    "        l[(i-1)*n+i]=0\n",
    "    end\n",
    "    return reshape(l,n,n)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "n=10\n",
    "distances=instanciaAleatoria(n)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "#finds greedy solution starting from ciudadInicio\n",
    "function greedy(distancias,n,ciudadInicio)\n",
    "    yavisitados=zeros(Bool,n)\n",
    "    distanciaAcumulada=0\n",
    "    actual=ciudadInicio\n",
    "    yavisitados[ciudadInicio]=true\n",
    "    tour=[actual]\n",
    "    #println(\"empiezo en \",actual)\n",
    "    for ciudadesVisitadas in 2:n\n",
    "        #buscar la ciudad mas cercana a actual no visitada \n",
    "        distanciaActual=10000000\n",
    "        siguiente=0\n",
    "        for i in 1:n\n",
    "            if yavisitados[i]==false #no visitado\n",
    "                if distanciaActual>=distancias[actual,i] #mas cercano?\n",
    "                    distanciaActual=distancias[actual,i] # lo guardo\n",
    "                    siguiente=i\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "        # siguiente tiene el vertice mas cercano al actual\n",
    "        distanciaAcumulada += distanciaActual\n",
    "        actual=siguiente\n",
    "        push!(tour, actual)\n",
    "        yavisitados[actual]=true\n",
    "        #println(\"paso a \",actual,\" con distancia acumulada \",distanciaAcumulada)\n",
    "    end\n",
    "    distanciaAcumulada += distancias[actual,1]\n",
    "    #distanciaAcumulada = distanciaAcumulada + distancias[actual,1]\n",
    "    return distanciaAcumulada, tour\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "#helper for local search (takes permutation and finds length of this tour)\n",
    "function evaluate(distancias,n,tour)\n",
    "    distanciaAcumulada=0\n",
    "    for i in 1:n-1\n",
    "        distanciaAcumulada += distancias[tour[i],tour[i+1]]\n",
    "    end\n",
    "    distanciaAcumulada += distancias[tour[n],tour[1]]\n",
    "    return distanciaAcumulada\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "#simple local search. Neighborhood is defined as the removal and insertion of a vertex\n",
    "function localsearch(distancias,n,tour,length)\n",
    "    mejora=true\n",
    "    while mejora\n",
    "        mejora=false\n",
    "        for i in 1:n\n",
    "            base=filter(x->x!=i,tour) #removes i from route and store in base\n",
    "            #println(\"base: \",base)\n",
    "            for position in 1:n\n",
    "                newRoute=deepcopy(base)\n",
    "                insert!(newRoute,position,i)\n",
    "                #println(\"nr: \",newRoute)\n",
    "                newObj=evaluate(distancias,n,newRoute)\n",
    "                if newObj<length\n",
    "                    #println(\" newObj: \",newObj,\" \",newRoute)\n",
    "                    length=newObj\n",
    "                    tour=deepcopy(newRoute)\n",
    "                    mejora=true\n",
    "                    break\n",
    "                end\n",
    "            end\n",
    "        end            \n",
    "    end\n",
    "    return length,tour\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "#finds n different starting solutions and applies lo9cal search to each of them\n",
    "for i in 1:n\n",
    "    distancia,tour=greedy(distances,n,i)\n",
    "    print(\"initial length: \",distancia)\n",
    "    distancia,tour=localsearch(distances,n,tour,distancia)\n",
    "    println(\" after local search: \",distancia)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "#helpers for exact model\n",
    "# extractTour provides a subtour according to current ip solution (sol)\n",
    "function extractTour!(n, initial,sol,inCircuit)\n",
    "    tour = [initial]  # Start at initial city\n",
    "    cur_city = initial\n",
    "    numVisited=0\n",
    "    while true\n",
    "        # Look for outgoing arc\n",
    "        for j = 1:n\n",
    "            if sol[cur_city,j] >= 0.9 #value 1\n",
    "                # Found next city\n",
    "                cur_city=j\n",
    "                push!(tour, j)\n",
    "                inCircuit[j]=1\n",
    "                # Mark as visited\n",
    "                numVisited += 1\n",
    "                break\n",
    "            end\n",
    "        end\n",
    "        # If we have come back to initial city, stop\n",
    "        if cur_city == initial\n",
    "            break\n",
    "        end\n",
    "    end  # end while\n",
    "    println(\"tour: \",tour,\" numVisited \",numVisited)\n",
    "    return tour, numVisited, inCircuit\n",
    "end\n",
    "\n",
    "# reports solution according to current ip solution (sol)\n",
    "function reportSolution(n, initial,sol)\n",
    "    tour = [initial]  # Start at initial city\n",
    "    cur_city = initial\n",
    "    while true\n",
    "        # Look for first arc out of current city\n",
    "        for j = 1:n\n",
    "            if sol[cur_city,j] >= 0.9 #value 1\n",
    "                # Found next city\n",
    "                cur_city=j\n",
    "                push!(tour, j)\n",
    "                # Mark as visited\n",
    "                break\n",
    "            end\n",
    "        end\n",
    "        # If we have come back to initial city, stop\n",
    "        if cur_city == initial\n",
    "            break\n",
    "        end\n",
    "    end  # end while\n",
    "    return tour\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "#builds model and adds lazy constraint\n",
    "\n",
    "function solveATSP(distances, n::Int64)\n",
    "    initialtime=time()\n",
    "    m =Model(CPLEX.Optimizer) \n",
    "    set_parameter(m,\"CPX_PARAM_SCRIND\",0)\n",
    "    \n",
    "    @variable(m, x[1:n,1:n], Bin)\n",
    "\n",
    "    @objective(m, Min, sum(distances[i,j]*x[i,j] for i in 1:n for j in 1:n))\n",
    "    @constraint(m, outgoing[i=1:n],x[i,i] == 0)\n",
    "    @constraint(m, origin[i = 1:n], sum(x[i,1:n]) == 1) # $sum_{j=1}^n x_{ij}=1 \\forall i$\n",
    "    @constraint(m, destination[j = 1:n], sum(x[1:n,j]) == 1) # $sum_{i=1}^n x_{ij}=1 \\forall j$\n",
    "    \n",
    "    function lazyGenerator(cb)\n",
    "        println(\"---- Inside subtour callback\")\n",
    "        inCircuit=fill(false,n)\n",
    "        x_val = callback_value.(Ref(cb), x)\n",
    "        for city in 1:n\n",
    "            if inCircuit[city]==0 \n",
    "                # sum arcs in subtour <= |cities| -1\n",
    "                subtour, numVisited,inCircuit = extractTour!(n, city,x_val,inCircuit)\n",
    "                if numVisited == n\n",
    "                    # This \"subtour\" is actually all cities, so we are done\n",
    "                    println(\"Solution visits all cities\")\n",
    "                    return\n",
    "                end\n",
    "                arcs_from_subtour = zero(AffExpr)\n",
    "                for i =1:numVisited\n",
    "                    arcs_from_subtour += x[subtour[i],subtour[i+1]]\n",
    "                end\n",
    "                MOI.submit(m, MOI.LazyConstraint(cb), @build_constraint(arcs_from_subtour <= numVisited-1))\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    MOI.set(m, MOI.LazyConstraintCallback(), lazyGenerator)\n",
    "    optimize!(m)\n",
    "    println(\"total time: \",time()-initialtime)\n",
    "    solution = reportSolution(n, 1,JuMP.value.(x))\n",
    "    println(solution,\" with length: \",getobjectivevalue(m))                    \n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "#solves model\n",
    "\n",
    "solveATSP(distances,n)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.5.1",
   "language": "julia",
   "name": "julia-1.5"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.5.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
